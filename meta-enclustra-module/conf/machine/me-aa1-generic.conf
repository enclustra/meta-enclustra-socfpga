#@TYPE: Machine
#@NAME: Intel SoCFPGA Arria10
#@DESCRIPTION: Machine configuration for the Arria 10 SoCFPGA

## override preamble
MACHINEOVERRIDES =. "${@['', 'me-aa1-generic:']['me-aa1-generic' != '${MACHINE}']}"

require conf/machine/include/socfpga.inc

SPL_BINARY = "spl/u-boot-splx4.sfp"
UBOOT_BINARY = "u-boot.img"

UBOOT_CONFIG ??= "sdmmc"
UBOOT_CONFIG[sdmmc] = "socfpga_enclustra_mercury_aa1_defconfig"
UBOOT_CONFIG[emmc]  = "socfpga_enclustra_mercury_aa1_defconfig"
UBOOT_CONFIG[qspi] = "socfpga_enclustra_mercury_aa1_defconfig"

KERNEL_IMAGETYPE = "uImage"
KERNEL_EXTRA_ARGS += "LOADADDR=0x8000"

IMAGE_FSTYPES = "cpio.gz.u-boot wic.bmap tar.gz"

EXTRA_IMAGEDEPENDS += "\
    device-tree \
    u-boot-socfpga \
    hw-ref-design \
"

## NB: use boot.scr due to common u-boot environment, even if there
## are other variables for u-boot.scr around:
##     boot_scripts=boot.scr.uimg boot.scr
IMAGE_BOOT_FILES ?= " \
    devicetree.dtb \
    socfpga_enclustra_mercury_emmc_overlay.dtbo \
    socfpga_enclustra_mercury_qspi_overlay.dtbo \
    socfpga_enclustra_mercury_sdmmc_overlay.dtbo \
    boot.scr \
    bitstream.itb \
    ${UBOOT_BINARY} \
    ${KERNEL_IMAGETYPE} \
"

WKS_FILE ?= "sdimage-aa1.wks"
IMAGE_FSTYPES:append = " wic"

SERIAL_CONSOLES ?= "115200;ttyS0"


## override postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' me-aa1-generic']['me-aa1-generic' != "${MACHINE}"]}"
